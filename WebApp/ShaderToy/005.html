<!DOCTYPE html>
<html lang="en">

<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Document</title>
</head>

<body>
    <script id="vs" type="x-shader/x-vertex">#version 300 es
        in vec4 a_position;
             void main() {
               gl_Position = a_position;
             }
             </script>
    <script id="fs" type="x-shader/x-fragment">#version 300 es
        precision highp float;
        uniform vec3 iResolution;
        uniform float iTime;
        uniform vec3 iChannelResolution[1];
        uniform vec4 iDate;
        uniform vec4 iMouse;  
        uniform sampler2D iChannel0;
        uniform sampler2D iChannel1;
        uniform sampler2D iChannel2;
        uniform sampler2D iChannel3;
        uniform int iFrame;
        uvec3 pcg3d(uvec3 v) {
    v = v * 1664525u + 1013904223u;
    v.x += v.y * v.z; v.y += v.z * v.x; v.z += v.x * v.y;
    v ^= v >> 16u;
    v.x += v.y * v.z; v.y += v.z * v.x; v.z += v.x * v.y;
    return v;
}

vec3 pcg33(vec3 p)
{
    uvec3 r = pcg3d(floatBitsToUint(p));
    return vec3(r) / float(0xffffffffu);
}

vec3 pcg23(vec2 p)
{
    return pcg33(p.xyx);
}

#define PI 3.1415926536

mat2 rot(float a)
{
    float s = sin(a);
    float c = cos(a);
    return mat2(c, s, -s, c);
}

vec3 star(vec2 uv, vec2 id)
{
    vec3 r = pcg23(id);
    float size = r.z;
    uv += (r.xy - .5);
    float d = length(uv);
    float l = size * .0002 / (d * d);
    float s1 = min(1., (.002 / abs(uv.x)) + (.002 / abs(uv.y)));
    uv *= rot(PI * .25);
    float s2 = min(1., (.002 / abs(uv.x)) + (.002 / abs(uv.y)));
    float s = s1 * smoothstep(.25, 0., d) + s2 * smoothstep(.15, 0., d);
    s *= smoothstep(.95, 1., size) * .5;
    float b = l +  .5 * s;
    b *= smoothstep(1., .5, d);
    float blink_i = smoothstep(0., .8, fract(r.z * 1000.));
    float blink_f = smoothstep(0., 1., fract(r.z * 100.));
    float blink = (.5+.5*sin(blink_f * iTime * 10.))*(blink_i*.5);
    return b * (1.0 - blink) * vec3(
        .5*smoothstep(1., .5, size)+.5,
        (.25 + .75*smoothstep(0., 0.5, size)) * (.5+.5*smoothstep(1., 0.5, size)) ,
        smoothstep(0., .5, size));
        
}

vec3 layer(vec2 uv, vec2 id)
{
    vec3 col = vec3(0);
    for (float x = -1.; x <= 1.; x++)
    {
        for (float y = -1.; y <= 1.; y++)
        {
            vec2 off = vec2(x, y);
            col += star(uv - off, id + off);
        }
    }
    return col;
}

#define N 6.
void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
    vec2 uv = (2. * fragCoord - iResolution.xy) / iResolution.y;
    vec3 col = vec3(0);
    float t = iTime * .05;
    
    uv *= rot(t);
    
    for (float f = 0.; f < 1.; f += 1. / N)
    {
        float depth = fract(f + t);
        float scale = mix(10., .1, depth);
        vec2 uv2 = uv * scale;
        uv2 += vec2(123.45, 55.22) * f;
        vec2 gv = fract(uv2) - .5;
        vec2 id = floor(uv2);
        col += depth * layer(gv, id) * smoothstep(1., .95, depth);
    }
    
    col = pow(col, vec3(0.4545));
    fragColor = vec4(col, 1.);
}
        out vec4 outColor;
        void main() {
          mainImage(outColor, gl_FragCoord.xy);
        }
        </script>
    <script src="gl.js"></script>
</body>

</html>